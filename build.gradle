buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.netflix.nebula:gradle-ospackage-plugin:3.6.1"
  }
}

apply plugin: "nebula.ospackage"

// Populate version information
String versionContents = new File("VERSION").text.trim()
def values = versionContents.split('-')

project.ext.set("buildVersion", values[0])
project.ext.set("buildRelease", values[1])

String versionString = "dtf-" + values[0] + "-" + values[1]

if ( values.length == 3) {
    project.ext.set("buildBranch", values[2])
    project.ext.set("versionString", versionString + "." + values[2])
} else {
    project.ext.set("buildBranch", "")
    project.ext.set("versionString", versionString)
}

// Date
String currentDate = new Date().format( 'yyyy-MM-dd' )

project.ext.set("currentDate", currentDate)

task makeDeb(type:Deb) {

    packageName = 'android-dtf'
    version = project.buildVersion
    release = project.buildRelease
    maintainer = 'Jake Valletta <javallet@gmail.com>'
    url = 'https://github.com/jakev/dtf'
    summary = 'Android Device Testing Framework (dtf)'
    packageDescription = 'Modular framework for discovering vulnerabilities on Android devices.'
    packageGroup = 'main'

    configurationFile('/etc/bash_completion.d/dtf.bash')

    requires('python')
    requires('python-lxml')
    requires('xz-utils')
    requires('sqlite3')

    into 'build/'

    /* Library Files */
    from('python-dtf/build/usr/local/lib') {
        fileMode 0644
        into '/usr/local/lib'
    }

    /* Launcher */
    from('python-dtf/build/usr/local/bin') {
        fileMode 0755
        into '/usr/local/bin'
    }

    /* Bash Completition */
    from('python-dtf/build/etc') {
        fileType CONFIG
        fileMode 0644
        into '/etc'
    }

    /* dtf Bindings */
    from('dtf-binding/') {
        fileMode 0755
        into '/usr/local/bin'
    }

    /* Binder Links */
    link('/usr/local/bin/dtf_aapt', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_abe', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_apktool', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_axmlprinter2', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_baksmali', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_dex2jar', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_shell', '/usr/local/bin/dtfbinder', 0755)
    link('/usr/local/bin/dtf_smali', '/usr/local/bin/dtfbinder', 0755)

    /* Man pages */
    from('man/') {
        into '/usr/local/man/man1/'
        fileMode 0644
        include '**/*.1'
    }

    from('man/') {
        into '/usr/local/man/man7/'
        fileMode 0644
        include '**/*.7'
    }
}

// Run all tests
task doPythonTests() {}

// Build Python files
task buildPython(type:Exec) {

    workingDir 'python-dtf'

    commandLine 'python', 'setup.py', 'bdist_dumb'
}

// Task to generate documentation
task genDocs(type:Exec) {

    mkdir('python-dtf/api-docs')

    workingDir 'python-dtf/api-docs'

    commandLine 'bash', '-c', 'pydoc -w ../dtf/*.py'
}

// Flake8 the Code
task doFlake(type:Exec) {

    workingDir 'python-dtf'

    commandLine 'flake8', './dtf'
}

// pylint the Code
task doLint(type:Exec) {

    workingDir 'python-dtf'

    commandLine 'pylint', './dtf', '--rcfile=./pylint.config'
}

// Run checkbashisms
task doCheckBashisms {
    doLast {
      exec {
        workingDir 'python-dtf/included'
        commandLine 'bash', '-c', 'checkbashisms -f ./*.sh'
      }
      exec {
        workingDir 'dtf-binding'
        commandLine 'bash', '-c', 'checkbashisms -f ./dtfbinder'
      }
    }
}

// Run shellcheck
task doShellCheck {
    doLast {
      exec {
        workingDir 'python-dtf/included'
        commandLine 'bash', '-c', 'shellcheck ./*.sh'
      }
      exec {
        workingDir 'dtf-binding'
        commandLine 'bash', '-c', 'shellcheck ./dtfbinder'
      }
    }
}

task deleteIncludedTar(type:Delete) {

    delete 'python-dtf/dtf/included.tar'
}

// Create TAR bundle of included
task tarIncluded(type:Tar) {

    from ('python-dtf/included/')
    include '**/*'
    into ''
    archiveName 'included.tar'
    destinationDir file('python-dtf/dtf/')
}

// Copy version file
task copyVersion(type:Copy) {

    from('.')
    into 'python-dtf/dtf/'
    include('VERSION')
}

// Generate the globals file
task generateGlobals(type:Exec) {

    workingDir 'python-dtf'
    commandLine './gen_globals.py'
}

// Make sure dtfClient APK exists
task assertClientApk() {
    doLast {
        if (!new File('dtf-client-app/app/build/outputs/apk').exists()) {
            throw new GradleException('dtfClient APK is not found. Are you sure you built the project?')
        }
    }
}

// Make sure man pages exist
task assertMans() {
    doLast {
        if (new File('man').listFiles().count { it.name ==~ /.*[1-9]$/ } == 0) {
            throw new GradleException('man pages are not processed! Process with generateMans')
        }
    }
}

// Copy the dtfClient APK
task copyClientApk(type:Copy) {

    from('dtf-client-app/app/build/outputs/apk/') {
        include '*-release.apk'
    }

    into 'python-dtf/included/dtfClient/'
}

// Run `ronn` on man pages
task generateMans(type:Exec) {

    workingDir 'man'

    commandLine 'bash', '-c', "ronn ./*.ronn --date=" + project.currentDate + " --organization=" + project.versionString + " --manual=\"dtf Manual\" --roff"

}

// Remove generated man pages
task cleanMans(type:Delete) {

    delete fileTree('man/') {
        include '*.1'
        include '*.7'
    }
}


// Clean up
clean {

    delete 'build'
    delete 'python-dtf/build'
    delete 'python-dtf/dist'
    delete "python-dtf/dtf.egg-info"
    delete 'python-dtf/api-docs'
    delete 'python-dtf/dtf/included.tar'
    delete 'python-dtf/dtf/VERSION'
    delete 'python-dtf/included/globals.ini'
    delete fileTree('python-dtf/included/dtfClient') {
        include '*.apk'
    }
}

// doTests will make sure the code passes all tests
doPythonTests.dependsOn doLint
doPythonTests.dependsOn doFlake

// Prepare the python code
buildPython.dependsOn doPythonTests
buildPython.dependsOn tarIncluded
buildPython.dependsOn copyVersion
tarIncluded.dependsOn generateGlobals
generateGlobals.dependsOn copyClientApk
copyClientApk.dependsOn assertClientApk

// Documentation requires valid Python
genDocs.dependsOn buildPython

// Bundling
tarIncluded.dependsOn deleteIncludedTar
tarIncluded.dependsOn doCheckBashisms
tarIncluded.dependsOn doShellCheck

// Building Debian
build.dependsOn makeDeb
makeDeb.dependsOn buildPython
makeDeb.dependsOn assertMans
